# Project: adams
# Date: 2020-06-26 14:33:31
# User: fracpete
# Charset: UTF-8
# Modules: adams-compress,adams-core,adams-imaging,adams-json,adams-net,adams-spreadsheet,adams-ufdl-core,adams-ufdl-image,adams-xml
#
adams.flow.control.Flow -annotation "Let\\\'s the user select files from a user-selected object detection to be displayed (incl. annotations).\\nUses host/user/password from the global UFDL Preferences." -flow-execution-listener adams.flow.execution.NullListener -flow-restart-manager adams.flow.control.flowrestart.NullManager
 adams.flow.standalone.CallableActors
  adams.flow.sink.DisplayPanelManager -display-type adams.flow.core.displaytype.Default -provider "adams.flow.sink.ImageViewer -display-type adams.flow.core.displaytype.Default -writer adams.gui.print.NullWriter -zoom -1.0 -show-properties true -selection-processor adams.gui.visualization.image.selection.NullProcessor -left-click-processor adams.gui.visualization.image.leftclick.ViewObjects -selection-shape-painter adams.gui.visualization.image.selectionshape.RectanglePainter -image-overlay \"adams.gui.visualization.image.ObjectLocationsOverlayFromReport -type-color-provider adams.gui.visualization.core.DefaultColorProvider -label-format \\\"# $\\\" -stroke-thickness 5.0\"" -entry-name-variable image -allow-search true
 adams.flow.standalone.UFDLConnection
 adams.flow.source.Start
 adams.flow.control.Trigger -name "select dataset"
  adams.flow.source.EnterManyValues -stop-if-canceled true -value "adams.flow.source.valuedefinition.UFDLObjectDetectionDatasetList -name dataset -display Dataset" -output-type MAP
  adams.flow.transformer.MapToVariables
  adams.flow.transformer.UFDLExtractAndTransfer -name dataset_pk -source dataset -target dataset_pk
 adams.flow.control.Trigger -name "list files"
  adams.flow.source.Variable -var-name dataset_pk -conversion adams.data.conversion.StringToInt
  adams.flow.transformer.UFDLTransformer -action adams.flow.transformer.ufdl.ListObjectDetectionFiles
  adams.flow.transformer.SpreadSheetSelectSubset -message "Please select images to display"
  adams.flow.transformer.SpreadSheetVariableRowIterator
  adams.flow.control.Tee -name "output files"
   adams.flow.transformer.SetVariable -name image -var-name image_file -var-value ${TMP}/@{dataset_pk}-@{image} -value-type FILE_FORWARD_SLASHES -expand-value true -output-value true
   adams.flow.transformer.Convert -conversion "adams.data.conversion.ReplaceFileExtension -extension .report -use-forward-slashes true"
   adams.flow.transformer.SetVariable -name report -var-name report_file
  adams.flow.control.Trigger -name "get file"
   adams.flow.source.Variable -var-name dataset_pk -conversion adams.data.conversion.StringToInt
   adams.flow.transformer.UFDLTransformer -action "adams.flow.transformer.ufdl.GetObjectDetectionFile -name @{image} -output-file @{image_file}"
  adams.flow.control.Trigger -name "get annotations"
   adams.flow.source.Variable -var-name dataset_pk -conversion adams.data.conversion.StringToInt
   adams.flow.transformer.UFDLTransformer -action "adams.flow.transformer.ufdl.GetObjectDetectionAnnotationsForImage -name @{image}"
   adams.flow.transformer.Convert -conversion adams.data.conversion.UFDLAnnotationsToReport
   adams.flow.transformer.ReportFileWriter -writer adams.data.io.output.DefaultSimpleReportWriter -generator "adams.core.io.SimpleFixedFilenameGenerator -name @{report_file}"
  adams.flow.control.Trigger -name display
   adams.flow.source.Variable -var-name image_file -conversion adams.data.conversion.StringToString
   adams.flow.transformer.ImageFileReader -reader adams.data.io.input.JAIImageReader -load-meta-data true -meta-data-reader adams.data.io.input.DefaultSimpleReportReader
   adams.flow.sink.CallableSink -callable DisplayPanelManager